
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言文档 - aardio + bat 批处理混合编程指南'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言文档 - aardio + bat 批处理混合编程指南</title> 
<link rel="stylesheet" href="../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio + bat 批处理混合编程指南</h1>

<p>本文我们主要介绍 aardio + bat 批处理混合编程。</p>

<h2>一、aardio + bat 入门</h2>

<p>请在 aardio 中点击「主菜单 / 新建工程 」打开工程向导，然后点击 「 更多 / 批处理」选择批处理工程模板，然后点击「 创建工程 」按钮。</p>

<p>生成的批处理工程例初始代码很简单，我们在工程中双击打开程序入口代码 main.aardio ，然后点击运行以查看运行后的效果。</p>

<p>范例工程的 main.aardio 源代码如下：</p>

<pre><code class="aardio language-aardio">import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var mainForm = win.form(text=&quot;aardio工程4&quot;;right=875;bottom=625)
mainForm.add(
btnExecute={cls=&quot;plus&quot;;text=&quot;执行批处理 1&quot;;left=433;top=549;right=605;bottom=594;align=&quot;left&quot;;bgcolor=-5197169;db=1;dr=1;font=LOGFONT(h=-16);iconStyle={align=&quot;left&quot;;font=LOGFONT(h=-16;name=&#39;FontAwesome&#39;);padding={left=20}};iconText=&#39;\uF17A&#39;;notify=1;textPadding={left=42};z=2};
btnExecute2={cls=&quot;plus&quot;;text=&quot;执行批处理 2&quot;;left=637;top=549;right=809;bottom=594;align=&quot;left&quot;;bgcolor=-5197169;db=1;dr=1;font=LOGFONT(h=-16);iconStyle={align=&quot;left&quot;;font=LOGFONT(h=-16;name=&#39;FontAwesome&#39;);padding={left=20}};iconText=&#39;\uF17A&#39;;notify=1;textPadding={left=42};z=3};
editResult={cls=&quot;edit&quot;;left=15;top=13;right=863;bottom=536;db=1;dl=1;dr=1;dt=1;edge=1;hscroll=1;multiline=1;vscroll=1;z=1}
)
/*}}*/

import process.batch;
mainForm.btnExecute.oncommand = function(id,event){

    ////优先调用 64 位命令请改为 process.batch.wow64 
    var prcs = process.batch(&quot;\bat\bat.aardio&quot;,{
        exepath = io._exepath; //传命名参数给批处理内的 aardio 代码，使用 owner.exepath 接收
        &quot;批处理启动参数1&quot;; //批处理使用 %1 接收第一个参数
        &quot;批处理启动参数2&quot;; //批处理使用 %2 接收第一个参数
    })

    mainForm.btnExecute.disabledText = {&#39;\uF254&#39;;&#39;\uF251&#39;;&#39;\uF252&#39;;&#39;\uF253&#39;;&#39;\uF250&#39;;text=&#39;&#39;}

    //out 用于接收批处理的全部输出(包含错误输出), err 为错误信息（无错误为 null ）
    var out,err = prcs.readAll(); //可在参数 @1 中指定匹配模式查找指定字符串
    mainForm.editResult.print(out);

    mainForm.btnExecute.disabledText = null;
}

mainForm.btnExecute2.oncommand = function(id,event){
    var prcs = process.batch(&quot;\bat\bat2.aardio&quot;,{
        text = &quot;abc,传参数给批处理&quot;; //传命名参数给批处理内的 aardio 代码，使用 owner.text 接收
        &quot;批处理启动参数1&quot;; //批处理使用 %1 接收第一个参数
        &quot;批处理启动参数2&quot;; //批处理使用 %2 接收第一个参数
    })

    mainForm.btnExecute2.disabledText = {&#39;\uF254&#39;;&#39;\uF251&#39;;&#39;\uF252&#39;;&#39;\uF253&#39;;&#39;\uF250&#39;;text=&#39;&#39;}

    //out 用于接收批处理的全部输出(包含错误输出), err 为错误信息（无错误为 null ）
    var out,err = prcs.readAll(); //可在参数 @1 中指定匹配模式查找指定字符串
    mainForm.editResult.print(out);

    mainForm.btnExecute2.disabledText = null;   
}

import style;
mainForm.btnExecute.skin(style.primaryButton);
mainForm.btnExecute2.skin(style.primaryButton);

mainForm.show();
win.loopMessage();
</code></pre>

<p>代码很简单，我们重点说一下上面代码中启动批处理的代码：</p>

<pre><code class="aardio language-aardio">var prcs = process.batch(&quot;\bat\bat.aardio&quot;,{
  exepath = io._exepath; //传递命名参数
  &quot;批处理启动参数1&quot;; //批处理使用 %1 接收第一个参数
  &quot;批处理启动参数2&quot;; //批处理使用 %2 接收第一个参数
})
</code></pre>

<p>process.batch() 函数用于启动批处理，返回一个进程管道对象。如果需要优先执行 64 位命令 —— 请改为 process.batch.wow64()，其他用法一样。  </p>

<p>先看参数 @1 指定的 bat 文件路径参数， aardio 中文件路径「首字符」可以用一个单斜杆（或反斜杆）表示应用程序根目录，应用程序根目录在开发时指工程目录或工程外独立启动的文件所在目录，发布后指 EXE 所在目录。</p>

<p>而 aardio 工程中的目录可以指定为「内嵌资源」，也就是将该目录下面的文件编译到 EXE 资源内。aardio 中很多文件有关的函数都自动兼容资源文件，代码不需要修改，上面的 <code>&quot;\bat\bat.aardio&quot;</code> 就是一个资源文件。</p>

<p><code>&quot;\bat\bat.aardio&quot;</code>的文件后缀改成 *.bat 后缀也是可以的，不过使用 *.aardio 后缀可以直接在 aardio 中编辑。我们可以右键点击 <code>&quot;\bat\bat.aardio&quot;</code>，然后在弹出菜单中点击「跳转到文件」，打开的批处理代码如下：</p>

<pre><code class="aardio language-aardio">?&gt;
@echo off 
for %%i in (&lt;?

//这里可以嵌入 aardio 代码，使用 print 函数动态生成批处理代码
import fsys;
fsys.enum( &quot;/&quot;, &quot;*.*&quot;,
    function(dir,filename,fullpath,findData){ 
        if(filename){ 
            print(filename,&quot; &quot;)
        }
        else {

        }
    },false
);

?&gt;) do echo %%i 

echo 批处理工作目录：&quot;%cd%&quot;


echo &lt;?= time() ?&gt;
echo &lt;?= owner.exepath ?&gt;

echo 此批处理接收到的第一个参数：&quot;%1&quot;
echo 此批处理接收到的第二个参数：&quot;%2&quot;

rem 批处理延时
ping 127.0.0.1 -n 2 &gt;nul

rem 下面自定义批处理进程退出代码
exit /B 123
</code></pre>

<p>我们可以在批处理中编写 aardio 代码，遵守类PHP的 aardio 模板语法即可 —— 也就是将 aardio 代码置入<code>&lt;? ?&gt;</code> 模板标记就可以了。</p>

<p>aardio将 <code>&lt;? ..... ?&gt;</code> 之外的部分解析为：<code>print(&quot;批处理代码&quot;)</code> 以调用全局函数 print 输出批处理代码。print 函数可以接收多个参数，每个参数都会自动调用 <code>tostring()</code> 转换为字符串。</p>

<p>可以使用 <code>&lt;?=表达式?&gt;</code> 输出文本，该代码的作用类似于 <code>print( 表达式 )</code> , 下面的写法也是允许的</p>

<pre><code class="aardio language-aardio">&lt;?
= 表达式
?&gt;
</code></pre>

<p>我们再回到开始，看一下启动该批处理的 aardio 代码：</p>

<pre><code class="aardio language-aardio">var prcs = process.batch(&quot;\bat\bat.aardio&quot;,{
  exepath = io._exepath; //传递命名参数
  &quot;批处理启动参数1&quot;; //批处理使用 %1 接收第一个参数
  &quot;批处理启动参数2&quot;; //批处理使用 %2 接收第一个参数
})
</code></pre>

<p><code>process.batch()</code> 用于启动批处理，第一个参数指定批处理文件（ 或者直接指定批处理代码也可以 ），后面可以用一个表参数指定批处理调用参数：</p>

<pre><code class="aardio language-aardio">{
  exepath = io._exepath; //传递命名参数
  &quot;批处理启动参数1&quot;; //批处理使用 %1 接收第一个参数
  &quot;批处理启动参数2&quot;; //批处理使用 %2 接收第一个参数
}
</code></pre>

<p>这个批处理表参数的数组成员传递为批处理的普通参数 —— 可以在批处理中用 <code>%1, %2</code> 等接收对应参数：</p>

<pre><code class="bat language-bat">echo 此批处理接收到的第一个参数：&quot;%1&quot;
echo 此批处理接收到的第二个参数：&quot;%2&quot;
</code></pre>

<p>而表参数中的名值对成员则传为 aardio 模板参数，在 <code>&quot;\bat\bat.aardio&quot;</code> 内可以用 owner 参数接收该模板参数，例如该文件内可以用 aardio 代码 owner.exepath 获取调用参数 exepth 。</p>

<pre><code class="aardio language-aardio">?&gt;
echo &lt;?= owner.exepath ?&gt;
</code></pre>

<p>process.batch() 返回一个进程管道对象 —— 也就是 process.popen 对象。使用该管道对象可以方便地读写批处理进程，获取返回值，退出代码等等。更多用法请参考标准库 process.popen 库函数文档。</p>

<p>其实用法很简单，例如等待批处理执行完成，并返回进程输出：</p>

<pre><code class="aardio language-aardio">var out,err = prcs.readAll(); //可在参数 @1 中指定匹配模式查找指定字符串
</code></pre>

<p>返回值 out 用于接收批处理的输出( 包含错误输出 )，返回值 err 用于接收批处理的错误输出。批处理调用&quot; EXIT /B 数值&quot;即可指定退出代码。</p>

<h2>二、aardio 与批处理简单对比</h2>

<p>本节提到的所有源码来自 aardio 自带范例。</p>

<p>下面是一个 aardio 调用批处理 for 语句的示例：</p>

<pre><code class="aardio language-aardio">import console
import process.batch;

//批处理 for 遍历并拆分字符串
var bat = process.batch(`
@echo off
for %%i in (abc,def,xyz) do echo %%i
`)
console.log( bat.readAll() )
console.pause();
</code></pre>

<p>下面我们用纯 aardio 代码实现上面的功能：循环遍历用空格键、跳格键(tab)、逗号、分号或等号拆分出来的字符串，aardio 代码如下：</p>

<pre><code class="aardio language-aardio">import console;
for( line in string.lines(&quot;abc,def,xyz&quot;,&quot;[\s,;=]&quot;) ){
  console.log(line)
}
console.pause(true);
</code></pre>

<p>上面是一个典型的 for 循环语句。string.lines() 用于创建迭代器，string.lines() 的第 @2 个参数指定分隔符 —— 支持类正则表达式的 aardio 模式匹配语法（请参考语法文档）。注意 aardio 里循环变量名 line 不需要在前面加%%，也没有只能使用26个字母的限制。</p>

<p>下面我们再看一个 aardio 调用批处理 for 语句的例子：</p>

<pre><code class="aardio language-aardio">import console
import process.batch;

//创建一个测试文件，双引号内换行符会解释为 &#39;\n&#39;
string.save(&quot;/test.txt&quot;,&quot;abc,def
123,456&quot; )

//批处理 for 遍历并按行拆分字符串
var bat = process.batch(`
@echo off
for /f &quot;usebackq delims=, tokens=1,2&quot; %%i in (&quot;test.txt&quot;) do echo %%i,%%j
`)
/*
注意文件路径如果有空格必须包含在引号内
如果要用引号包含路径，就必须加上 usebackq，usebackq的意思是用反引号包含命令，
单引号包含字符串，然后双引号就可以包含文件路径而不是字符串了
*/

console.dump(bat.readAll())
console.pause()
</code></pre>

<p>用纯 aardio 代码这样写：</p>

<pre><code class="aardio language-aardio">
import console

//aardio 需要先读文件到字符串
var str = string.load(&quot;/test.txt&quot;)

//参数@3指定delims，可以用强大的模式匹配语法指定分隔符
for tokens in string.lines(str,,&quot;,&quot;) {
   /*
   tokens 是一个数组，可以用 string.join
   任意拼接数组中指定范围的元素实现批处理 tokens=n-m 的效果
   */
  console.log(tokens[1],tokens[2])
}

console.pause()
</code></pre>

<p>其实在 aardio 中还可以 string.each() 实现类似功能，如下：</p>

<pre><code class="aardio language-aardio">for a,b in string.each(str,&quot;([^,]+),(.+)&quot;){
  console.log(a,b)
}
</code></pre>

<p>再看一个例子，aardio 中调用 for 语句遍历文件这样写：</p>

<pre><code class="aardio language-aardio">import console;
import process.batch;

//批处理 for 遍历一个目录下的所有文件
var bat = process.batch(`
@for /r &quot;./&quot; %%I in (*) do @echo %%I
`)

for( all,out,err in bat.each() ){
   console.log(all)
}
console.pause()
</code></pre>

<p>改成纯 aardio 代码遍历文件这样写：</p>

<pre><code class="aardio language-aardio">import console;

//aardio 遍历一个目录下的所有文件
import fsys;
fsys.enum( &quot;/&quot;, &quot;*.*&quot;,
  function(dir,filename,fullpath,findData){
    if(filename){
          console.log(&quot;发现文件：&quot;+filename,&quot;完整路径：&quot;+fullpath)
    }
    else{
      console.log( &quot;发现目录：&quot; + dir )
    }
  }
);
console.pause();
</code></pre>

<h2>三、执行 CMD 命令与进程管道操作</h2>

<p>如果我们不需要执行 bat 批处理。也可以用 process.popen 直接调用 cmd.exe 创建进程管道。注意 process.batch 同样是基于 process.popen 调用 cmd.exe ，对进程管道的操作是一样的。</p>

<p>批理执行 CMD 命令：</p>

<pre><code class="aardio language-aardio">import process.popen

//打开命令行,隐藏命令行窗口
var prcs = process.popen.cmd(`
CD &quot;C:\Program Files&quot;
C:
dir
mkdir test
rmdir test
`)

//显示结果
import win;
win.msgbox(prcs.readAll())
</code></pre>

<p>process.popen 创建的进程对象不会打开黑窗口，而且可以通过返回的进程管道读写进程标准输出输入。当然这个方法不仅仅是可以用于 cmd.exe ，也适用于所有控制台程序。</p>

<p>再看一个读写进程管道的例子：</p>

<pre><code class="aardio language-aardio">//管道读写

import process.popen

/*
打开命令行,隐藏命令行窗口
如果包含弹出控制台窗口的外部命令，
请将下面的 process.popen.detached 换为 process.popen
*/
var prcs = process.popen.detached(&quot;cmd.exe&quot;)

//如果调用UTF8编码的程序，请添加下面的编码声明
//prcs.codepage = 65001

var cmd = /*
CD C:\
C:
dir 
mkdir test
rmdir test
*/

prcs.write(cmd)
var result = prcs.peekTo(&quot;&gt;&quot;);
prcs.print(&#39;exit&#39;)

//显示结果
import console;
console.log( result );
console.pause(&quot;pause&quot;);
</code></pre>

<h2>四、环境变量</h2>

<p>直接看范例：</p>

<pre><code class="aardio language-aardio">
import win;
import process.popen

//在父进程中指定环境变量
string.setenv(&quot;TESTENV&quot;,&quot;测试变量值&quot;);

//也可以用下面的方法
//import environment
//environment.user().set(&quot;TESTENV&quot;,&quot;测试变量值&quot;)

//打开命令行,隐藏命令行窗口
var prcs = process.popen.cmd(`echo %TESTENV%`) 

//也可以在 process 或 process.popen 参数@3中通过 environment为目标进程指定环境变量
var prcs = process.popen(&quot;cmd.exe&quot;,&quot;/c echo %TESTENV2%&quot;,{
  environment = {
    TESTENV2 = &quot;测试变量值2&quot;
  }
})

import fsys.environment;
import process.batch;
var prcs = process.batch( `
@echo off
set TESTENV3=测试变量值3&lt;?
  print( fsys.environment.expand(&quot;%appdata%&quot;) )

?&gt;&amp;
echo %TESTENV3%
`)

//显示结果
import win;
win.msgbox(prcs.read(-1))
</code></pre>

<h2>五、发送 Ctrl + C</h2>

<p>直接看范例：</p>

<pre><code class="aardio language-aardio">//发送Ctrl+C
import console
import process.popen

var prcs = process.popen(&quot;ping 127.0.0.1 -n 10 &quot;)
for( all,out,err in prcs.each() ){
    console.log( out,err ); 
    prcs.ctrlEvent(0);
}

console.pause();
</code></pre>

<p><a href="javascript:if(confirm('https://www.aardio.com/zh-cn/doc/library-guide/std/process/batch.md  \n\nļ޷ Teleport Ultra , Ϊ Ŀļ͹淶ڡ  \n\nڷϴ?'))window.location='https://www.aardio.com/zh-cn/doc/library-guide/std/process/batch.md'" tppabs="https://www.aardio.com/zh-cn/doc/library-guide/std/process/batch.md">Markdown 格式</a></p>

</body> 
</html>