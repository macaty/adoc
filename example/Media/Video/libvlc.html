
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言范例 - 万能视频播放器 - 调用 libvlc'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言范例 - 万能视频播放器 - 调用 libvlc</title> 
<link rel="stylesheet" href="../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio 范例: 万能视频播放器 - 调用 libvlc</h1><pre><code class="aardio language-aardio">import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text=&quot;万能视频播放器 - 调用 libvlc&quot;;right=856;bottom=483;bgcolor=16515070;)
winform.add(
btnFullScreen={cls=&quot;plus&quot;;text=&#39;\uF047&#39;;left=811;top=453;right=852;bottom=483;color=1938422;db=1;dr=1;font=LOGFONT(h=-16;name=&#39;FontAwesome&#39;);notify=1;repeat=&quot;center&quot;;z=4};
btnPlay={cls=&quot;plus&quot;;text=&#39;\uF04B&#39;;left=10;top=456;right=42;bottom=479;color=1938422;db=1;dl=1;font=LOGFONT(h=-19;name=&#39;FontAwesome&#39;);notify=1;repeat=&quot;scale&quot;;z=5};
lbTime={cls=&quot;plus&quot;;text=&quot;00:00:00&quot;;left=602;top=455;right=655;bottom=475;db=1;dr=1;z=7};
plus={cls=&quot;plus&quot;;text=&#39;\uF027&#39;;left=666;top=457;right=691;bottom=480;color=1938422;db=1;dr=1;font=LOGFONT(h=-19;name=&#39;FontAwesome&#39;);repeat=&quot;scale&quot;;z=2};
trackbar={cls=&quot;plus&quot;;left=49;top=460;right=595;bottom=472;bgcolor=-2512093;border={radius=-1};color=23807;db=1;dl=1;dr=1;foreRepeat=&quot;expand&quot;;foreRight=13;forecolor=-14911489;notify=1;paddingBottom=5;paddingTop=5;z=1};
video={cls=&quot;custom&quot;;left=8;top=6;right=848;bottom=447;bgcolor=0;db=1;dl=1;dr=1;dt=1;z=6};
volume={cls=&quot;plus&quot;;left=696;top=461;right=812;bottom=473;bgcolor=-2512093;border={radius=-1};color=23807;db=1;dr=1;foreRepeat=&quot;expand&quot;;foreRight=13;forecolor=-14911489;notify=1;paddingBottom=5;paddingTop=5;z=3}
)
/*}}*/

import libvlc; 

//初始化vlc引擎，可用参数：https://wiki.videolan.org/VLC_command-line_help/ 
var vlcEngine = libvlc(
    &quot;--subsdec-encoding=GB18030&quot;,//字幕默认使用中文编码
    _WINXP ? &quot;--freetype-font=宋体&quot; : &quot;--freetype-font=DFKai-SB&quot;,//WIN7可用字体：Arial Unicode MS,DFKai-SB, Microsoft JhengHei, MingLiU, MingLiU-ExtB, MingLiU_HKSCS, MingLiU_HKSCS-ExtB, PMingLiU, PMingLiU-ExtB
    &quot;-I&quot;,&quot;dummy&quot;,&quot;--ignore-config&quot;//,&quot;--extraintf=logger&quot; ,&quot;--verbose=2&quot; 
    )

//创建播放器
var vlcPlayer = vlcEngine.mediaPlayer( winform.video ); 
winform.video.orphanWindow();
vlcPlayer.setKeyInput(true);

var eventManager = vlcPlayer.getEventManager()
eventManager.mediaPlayerTimeChanged = function(event){ 
    if( !winform.trackbar.state.active ){
        winform.trackbar.progressPos = event.newTime;
    }   
} 

eventManager.mediaPlayerLengthChanged = function(event){ 
    winform.btnPlay.disabledText = null;
    winform.trackbar.setTrackbarRange(1,vlcPlayer.length);
    winform.trackbar.progressPos = 0;
    //vlcPlayer.spuLoad(&quot;字幕路径&quot;);
}  

import fsys.dlg;
winform.btnPlay.oncommand = function(id,event){  

    if( winform.btnPlay.checked ){ 
        if(!vlcPlayer.isStopped() &amp;&amp; !vlcPlayer.isPaused() &amp;&amp;  !vlcPlayer.isEnded() ){
            //vlcPlayer.loadMediaFile(fsys.dlg.open(,&quot;请指定视频文件&quot;)); 
            vlcPlayer.loadMedia(&quot;http://download.aardio.com/demo/video.aardio&quot;); 
            winform.btnPlay.disabledText = {&#39;\uF254&#39;;&#39;\uF251&#39;;&#39;\uF252&#39;;&#39;\uF253&#39;;&#39;\uF250&#39;}
        }

        vlcPlayer.play();   
    }
    else {
        vlcPlayer.pause(); 
    } 
} 

winform.volume.setTrackbarRange(1,100);
winform.trackbar.onMouseUp = function(){
    vlcPlayer.time = winform.trackbar.progressPos; 
} 

winform.trackbar.onPosChanged = function( pos,thumbTrack ){
    winform.lbTime.text = time(pos/1000,&quot;!%H:%M:%S&quot;);
}

winform.volume.setTrackbarRange(1,100);
winform.volume.progressPos = 100;
winform.volume.onMouseUp = function(){
    //不要写在 onPosChanged 事件里，过于密集调用这接口有可能会卡
    vlcPlayer.volume = winform.volume.progressPos ;
}

winform.btnFullScreen.oncommand = function(id,event){
    winform.video.fullscreen(true);
} 

// 关闭窗体时退出播放器
winform.onClose = function(hwnd,message,wParam,lParam){
    vlcPlayer.release()
}

//按回车、空格暂停
winform.isDialogMessage = function(hwnd,msg){ 
    if(  msg.wParam == 0xD/*_VK_RETURN*/ || msg.wParam == 0x20/*_VK_SPACE*/ ){
        msg.hwnd = winform.btnPlay.hwnd;
        return;
    }
    return win.isDialogMessage(hwnd,msg);
}

//全屏快捷键
winform.video.translateAccelerator = function(msg){ 
     if(  msg.wParam == 0xD/*_VK_RETURN*/ || msg.wParam == 0x20/*_VK_SPACE*/ ){
          msg.hwnd = winform.btnPlay.hwnd;
     }
     if(  msg.wParam == 0x1B/*_VK_ESC*/ ){
         winform.video.fullscreen(false); 
     }
}

winform.volume.skin({
    background={
        default=0xFF23ABD9
    };
    foreground={
        default=0xFFFF771C;
        hover=0xFFFF6600
    };
    color={
        default=0xFFFF5C00;
        hover=0xFFFF6600
    }
})

winform.trackbar.setTrackbarRange(1,100);
winform.trackbar.skin({
    background={
        default=0xFF23ABD9
    };
    foreground={
        default=0xFFFF771C;
        hover=0xFFFF6600
    };
    color={
        default=0xFFFF5C00;
        hover=0xFFFF6600
    }
})

winform.btnFullScreen.skin({
    color={
        active=0xFF00FF00;
        default=0xFFF6931D;
        disabled=0xFF6D6D6D;
        hover=0xFFFF0000
    }
})

winform.btnPlay.skin({
    color={
        active=0xFF00FF00;
        default=0xFFF6931D;
        disabled=0xFF6D6D6D;
        hover=0xFFFF0000
    }
    checked={
        text = &#39;\uF04D&#39;
    }
})

winform.show(); 
win.loopMessage();
</code></pre>
<a href="https://www.aardio.com/zh-cn/doc/example/Media/Video/libvlc.md">Markdown 格式</a>
</body> 
</html>