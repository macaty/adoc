[aardio æ–‡æ¡£](../../../index.htm "aardio ç¼–ç¨‹è¯­è¨€æ–‡æ¡£é¦–é¡µ")

# aardio èŒƒä¾‹: RUNAS//ç½‘å¡åˆ‡æ¢

```aardio aardio
//RUNAS//ç½‘å¡åˆ‡æ¢
import win.ui;
/*DSG{{*/
var winform = win.form(text="ç½‘ç»œè¿æ¥";right=537;bottom=206)
winform.add()
/*}}*/

if(!_STUDIO_INVOKED){
    if(!_ARGV.task){
        import sys.runAsTask;
        var task = sys.runAsTask("MyNetworkManager","ç½‘å¡è‡ªåŠ¨åˆ‡æ¢ç¨‹åº");
        task.register("/task");

        win.msgbox("æœ¬ç¨‹åºå·²è®¾ç½®ä¸ºç™»å½•æ—¶è‡ªåŠ¨ä»¥ç®¡ç†æƒé™è¿è¡Œï¼ˆæ— ç¡®è®¤å¯¹è¯æ¡†ï¼?);
        return;
    }
}

import inet.connView;
var wb = inet.connView(winform);
wb.shellFolderSelectionChanged = function(itemPath,itemName) {

}

var iconNetWork = ::User32.LoadIconP(::Shell32,18);
winform.setIcon(iconNetWork);

import win.util.tray;
winform.tray = win.util.tray(winform,iconNetWork);

winform.onMinimize = function(lParam){
    winform.show(false); //éšè—çª—å£
    return true;//é˜»å‡»é»˜è®¤æ¶ˆæ¯ä¼ é€?å–æ¶ˆæœ€å°åŒ–è¿‡ç¨‹
}

import win.ui.menu;
import sys.networkCards;
import com.wmi;

reloadNetworkAdapters = function(){
    winform.popmenu = win.ui.popmenu(winform);

    var adAct,adActMenuId;
    for networkCard in sys.networkCards.each(){
        var adCur = com.wmi.get("SELECT * FROM Win32_NetworkAdapter WHERE NetConnectionId =@netConnectionId",networkCard)
        if(!adCur){
            continue;
        }

        var idCur;
        idCur = winform.popmenu.add('åˆ‡æ¢åˆ°ï¼š' + networkCard.netConnectionId,function(){
            if(adAct){
                //æœ‰äº›é©±åŠ¨æˆ–ç¡¬ä»¶æœ‰é—®é¢˜çš„ç½‘å¡ï¼Œå¯èƒ½è¦ç”¨ process.devcon.disable() æ‰èƒ½ç¦ç”¨
                adAct.Disable(); //ç¦ç”¨ç½‘å¡
                winform.popmenu.check(adActMenuId,false,0/*_MF_BYCOMMAND*/);

                if(adAct == adCur){
                    adAct = null;
                    adActMenuId = null;
                    return;
                }
            }

            adAct = adCur;
            adCur.Enable(); //å¯ç”¨ç½‘å¡

            adActMenuId = idCur;
            winform.popmenu.check(adActMenuId,true,0/*_MF_BYCOMMAND*/);
        });

        if(adCur.NetEnabled){
             adAct = adCur;
             adActMenuId = idCur;
             winform.popmenu.check(idCur,true,0/*_MF_BYCOMMAND*/);
        }
    }

    winform.popmenu.add();
    winform.popmenu.add('æµè§ˆç½‘ç»œè¿æ¥â€¦â€?,function(){
        import process;
        process.explore("shell:::{7007ACC7-3202-11D1-AAD2-00805FC1270E}");
    });
    winform.popmenu.add('é€€å‡?,function(){ winform.close() });
}

winform.wndproc = {
    [0xACCF/*_WM_TRAYMESSAGE*/ ] = function(hwnd,message,wParam,lParam){
        if( lParam = 0x205/*_WM_RBUTTONUP*/ ){
            //å¼¹å‡ºæ‰˜ç›˜èœå•ä»¥å‰,ä¸€å®šè¦å‰ç½®ä¸»çª—å£ä¸­,ä¸ç„¶ä¸ç‚¹å‡»èœå•ä¸ä¼šæ¶ˆå¤?            win.setForeground(winform.hwnd)
            winform.popmenu.popup()
        }
        elseif( lParam = 0x203/*_WM_LBUTTONDBLCLK*/ ){
            winform.show(true);
        }
    }
}

reloadNetworkAdapters();
winform.show(true);
win.loopMessage();

```

[Markdown æ ¼å¼](https://www.aardio.com/zh-cn/doc/example/Network/Manage/connections.md)

