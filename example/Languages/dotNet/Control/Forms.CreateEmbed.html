
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言范例 - aardio 嵌入 .NET 数据表格'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言范例 - aardio 嵌入 .NET 数据表格</title> 
<link rel="stylesheet" href="../../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio 范例: 嵌入 .NET 数据表格</h1><pre><code class="aardio language-aardio">//aardio 嵌入 .NET 数据表格
import win.ui;
/*DSG{{*/
var winform = win.form(text=&quot;嵌入 .NET 控件&quot;;right=759;bottom=510)
winform.add(
button={cls=&quot;button&quot;;text=&quot;读取数据&quot;;left=495;top=439;right=739;bottom=504;color=14120960;db=1;dr=1;font=LOGFONT(h=-14);note=&quot;获取数据源中的数据&quot;;z=3};
custom={cls=&quot;custom&quot;;left=25;top=25;right=736;bottom=274;db=1;dl=1;dr=1;dt=1;z=1};
edit={cls=&quot;edit&quot;;left=26;top=290;right=737;bottom=435;autohscroll=false;db=1;dl=1;dr=1;edge=1;multiline=1;vscroll=1;z=2}
)
/*}}*/

import System;
import System.Data;
import System.Windows.Forms; 

//.NET 控件必须用 System.Windows.Forms.CreateEmbed 嵌入 aardio 窗口
var dataGridView = System.Windows.Forms.CreateEmbed(&quot;DataGridView&quot;,winform.custom);  
dataGridView.EditMode = 2; 

//添加数据列
var dataTable = System.Data.DataTable(&quot;DT&quot;); 
dataTable.Columns.Add(&quot;名称&quot;);//添加列
dataTable.Columns.Add(&quot;计数&quot;,System.Double); //添加指定数据类型的列
dataTable.Columns.Add(&quot;选择&quot;,System.Boolean); //自动显示复选框 

//绑定数据源到视图
var dataView = System.Data.DataView(dataTable);
dataGridView.DataSource = dataView;

//替换名字为&quot;名称&quot;的列为下拉框控件（参考 System.Windows.Forms 库源码）
dataGridView.ReplaceNameValueListColumn(&quot;名称&quot;,120/*宽度*/,
    { &quot;王五&quot;,&quot;张三&quot;,&quot;李四&quot;},
    { &quot;WangWu&quot;,&quot;ZhangSan&quot;,&quot;LiSi&quot;}
); 

//添加测试数据
var row = dataTable.NewRow(); 
row.ItemArray = {&quot;WangWu&quot;,123, true}
dataTable.Rows.Add(row);   

/*
可以用 FillFromArray 函数一次性填充数据。
避免密集调用 .NET 参数，减少不必要的跨语言交互成本。FillFromArray 实现请查看 System.Data.DataTable 源码。
*/
dataTable.FillFromArray({
    {&quot;ZhangSan&quot;,456,false};
    {&quot;LiSi&quot;,789,true}
})

//添加事件(event)
dataTable.ColumnChanged = function(sender,eventArgs){
    var columnName = eventArgs.Column.ColumnName;
    var value  = eventArgs.Row.getItem(columnName);  
    winform.edit.print(&quot;已改变列：&quot;,columnName,&quot; 已变更值：&quot;,value);
}

//读取数据
winform.button.oncommand = function(id,event){ 

    //一次读取所有数据到 aardio 数组
    var data = dataTable.ExtractToArray();

    //输出结果
    for(i=1;#data;1){  
        winform.edit.dump(  data[i]  )  
    }

    //修改数据
    data[1][2]= 999;

    //替换并更新原来的数据
    dataTable.FillFromArray(data,true)

    //下面是原始的方法，交互次数多一些
    /*
    for(i=1;dataTable.Rows.Count;1){ 
        var arr = dataTable.Rows[i].ItemArray; 
        winform.edit.print( arr[1] )    ; 
    } 
    */
}

winform.show();
win.loopMessage();
</code></pre>
<a href="https://www.aardio.com/zh-cn/doc/example/Languages/dotNet/Control/Forms.CreateEmbed.md">Markdown 格式</a>
</body> 
</html>