
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言范例 - 液晶计时器'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言范例 - 液晶计时器</title> 
<link rel="stylesheet" href="../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio 范例: 液晶计时器</h1><pre><code class="aardio language-aardio">//液晶计时器
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text=&quot;液晶计时器&quot;;left=-20;top=50;right=327;bottom=362;bgcolor=16777215;border=&quot;none&quot;;max=false)
winform.add(
bk={cls=&quot;bk&quot;;left=0;top=0;right=349;bottom=27;bgcolor=10789024;z=9};
btnClock={cls=&quot;plus&quot;;text=&quot;启动时钟&quot;;left=68;top=163;right=180;bottom=195;bgcolor=13234329;border={radius=6};notify=1;z=2};
btnColor={cls=&quot;plus&quot;;text=&#39;\uF013&#39;;left=287;top=83;right=312;bottom=108;border={radius=6};color=9868950;font=LOGFONT(name=&#39;FontAwesome&#39;;charset=0);notify=1;z=10};
btnCountDownTimer={cls=&quot;plus&quot;;text=&quot;启动倒计时&quot;;left=202;top=117;right=314;bottom=149;bgcolor=13234329;border={radius=6};notify=1;z=3};
btnPause={cls=&quot;plus&quot;;text=&#39;\uF04C&#39;;left=188;top=214;right=220;bottom=245;border={radius=6};color=2368548;font=LOGFONT(name=&#39;FontAwesome&#39;;charset=0);notify=1;z=7};
btnStop={cls=&quot;plus&quot;;text=&#39;\uF04D&#39;;left=243;top=214;right=275;bottom=245;border={radius=6};color=2368548;font=LOGFONT(name=&#39;FontAwesome&#39;;charset=0);notify=1;z=8};
btnTimer={cls=&quot;plus&quot;;text=&quot;启动计时器&quot;;left=200;top=163;right=312;bottom=195;bgcolor=13234329;border={radius=6};notify=1;z=4};
datetimepick={cls=&quot;datetimepick&quot;;left=35;top=121;right=190;bottom=147;edge=1;transparent=1;updown=1;z=5};
lbTip={cls=&quot;static&quot;;text=&quot;双击时间切换悬浮显示模式&quot;;left=35;top=264;right=322;bottom=288;transparent=1;z=6};
plusClock={cls=&quot;plus&quot;;left=60;top=33;right=279;bottom=93;notify=1;z=1}
)
/*}}*/

var style = {
    icon = {
        background={
            active=0xFF0078B0;
            hover=0xFF00AEFF;
            default=0;
        }; 
    };
    button = {
        background={
            active=0xFF0078B0;
            hover=0xFF00AEFF; 
            disabled=0xFFCCCCCC;
        }; 
    }
}

winform.btnStop.skin(style.icon) 
winform.btnPause.skin(style.icon)
winform.btnCountDownTimer.skin(style.button) 
winform.btnClock.skin(style.button)
winform.btnTimer.skin(style.button)

import win.ui.lcdClock;
var lcdClock = win.ui.lcdClock(winform.plusClock);
if(!lcdClock.setImageAttributes) error(&quot;请先更新 win.ui.lcdClock 扩展库&quot;,2)
lcdClock.startClock();

winform.onMinimize = function(lParam){
    winform.plusClock.orphanWindow(true)
    win.setTopmost(winform.plusClock.hwnd)
    winform.modifyStyleEx(0x40000/*_WS_EX_APPWINDOW*/,0x80/*_WS_EX_TOOLWINDOW*/);
    winform.transparent(0)
    winform.plusClock.floating = true;
    return true;
}

winform.plusClock.onMouseDown = function(wParam,lParam){
    if( winform.plusClock.floating ) {
        ::User32.SendMessage(owner.hwnd,0xA1/*_WM_NCLBUTTONDOWN*/,2/*_HTCAPTION*/,0);
    }   
    else {
        winform.hitCaption();
    }
}

winform.plusClock.onMouseDoubleClick = function(wParam,lParam){
    if( !winform.plusClock.floating ) {
        winform.hitMin();
        return;
    }

    winform.plusClock.floating = false;

    winform.transparent(false)
    win.setTopmost(winform.plusClock.hwnd,false)
    winform.modifyStyleEx(0x80/*_WS_EX_TOOLWINDOW*/,0x40000/*_WS_EX_APPWINDOW*/);
    winform.resize();
    winform.datetimepick.redraw();
}

winform.btnClock.oncommand = function(id,event){
    lcdClock.startClock()   
    winform.btnClock.disabled = true;
    winform.btnCountDownTimer.disabled = false;
    winform.btnTimer.disabled = false;
}

import fsys.media;
winform.btnCountDownTimer.oncommand = function(id,event){
    winform.btnClock.disabled = false;
    winform.btnCountDownTimer.disabled = true;
    winform.btnTimer.disabled = false

    lcdClock.startCountDownTimer(,function(){
        winform.btnCountDownTimer.disabled = false;
        fsys.media.playSound(&quot;C:\Windows\media\Ring05.wav&quot;)
    })
}

winform.btnTimer.oncommand = function(id,event){
    lcdClock.startTimer();

    winform.btnClock.disabled = false;
    winform.btnCountDownTimer.disabled = false;
    winform.btnTimer.disabled = true;
}

winform.btnPause.oncommand = function(id,event){
    lcdClock.pause();

    winform.btnClock.disabled = false;
    winform.btnCountDownTimer.disabled = false;
    winform.btnTimer.disabled = false;
}

winform.datetimepick.setFormat(&quot;&#39; 设置倒计时:&#39;HH&#39;:&#39;mm&#39;:&#39;ss&quot;);
winform.datetimepick.time = time.iso8601(0);
winform.datetimepick.onnotify = function(id,code,ptr){
    if(code == 0xFFFFFD09/*_DTN_DATETIMECHANGE*/){ 
        lcdClock.resetCountDownTimer( winform.datetimepick.time )
        winform.btnClock.disabled = false;
        winform.btnCountDownTimer.disabled = false;
        winform.btnTimer.disabled = false
    }
}

winform.btnStop.oncommand = function(id,event){
    lcdClock.stop();

    winform.btnClock.disabled = false;
    winform.btnCountDownTimer.disabled = false;
    winform.btnTimer.disabled = false;
}

import win.ui.simpleWindow2;
var simpleWindow2 = win.ui.simpleWindow2(winform);

import win.ui.tooltip; 
var tooltipCtrl = win.ui.tooltip( winform );//在窗口上创建tooltip控件 
tooltipCtrl.addTool(winform.btnStop,&quot;停止计时&quot; )
tooltipCtrl.addTool(winform.btnPause,&quot;暂停计时&quot; ) 
tooltipCtrl.addTool(winform.btnClock,&quot;显示时钟，不会清零计时器&quot; ) 
tooltipCtrl.addTool(simpleWindow2.titlebarMin,&quot;点这里隐藏窗口并悬浮显示计时器&quot; )  
tooltipCtrl.addTool(winform.plusClock,&quot;鼠标左键按住可以拖动,双击可以切换显示模式&quot; )

import fsys.config;
var config = fsys.config(io.appData(&quot;aardio/std/lcdClock&quot;))
if(config.setting.color){
    lcdClock.setColor(config.setting.color);
}

winform.btnColor.skin({
    background={
        active=0xFF0078B0;
        hover=0xFF00AEFF;
    }; 
})

import win.ui.ctrl.pick;
winform.btnColor.oncommand = function(id,event){
    var picker = win.ui.ctrl.pick(winform);

    if(config.setting.color){
        picker.setColor(config.setting.color);
    }
    picker.onColorChange = function(argb){
        lcdClock.setColor(argb);
        config.setting.color = argb;
    }

    picker.onInitDialog = function(){
        win.center(picker.hwnd,winform.hwnd);
        picker.top = winform.top + winform.btnColor.bottom;
    }

    picker.doModal()
}

winform.show() 
win.loopMessage();
</code></pre>
<a href="https://www.aardio.com/zh-cn/doc/example/aardio/DateTime/clock.md">Markdown 格式</a>
</body> 
</html>