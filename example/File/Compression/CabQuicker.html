
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言范例 - CabQuicker -  压缩打包工具'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言范例 - CabQuicker -  压缩打包工具</title> 
<link rel="stylesheet" href="../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio 范例: CabQuicker -  压缩打包工具</h1><pre><code class="aardio language-aardio">//Cab 打包压缩
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text=&quot;CabQuicker -  压缩打包工具&quot;;right=599;bottom=399;parent=...)
winform.add(
tab={cls=&quot;tab&quot;;left=9;top=8;right=587;bottom=389;db=1;dl=1;dr=1;dt=1;edge=1;z=1}
)
/*}}*/

var frmCompress,frmUnCompress;

/*压缩工具{{*/
frmCompress = winform.tab.add( text=&quot;压缩&quot;;acceptfiles=1;bottom=363;parent=...;right=590 )
frmCompress.add( 
editDst={ dl=1;bottom=147;right=344;left=41;dt=1;top=120;z=3;text=&quot;/test.cab&quot;;edge=1;cls=&quot;edit&quot; };
btnCompress={ dr=1;bottom=345;text=&quot;生成压缩文件&quot;;font=LOGFONT(name=&#39;FontAwesome&#39;);left=393;top=305;z=8;db=1;right=553;cls=&quot;button&quot; };
static={ dl=1;bottom=48;right=309;left=41;dt=1;top=26;transparent=1;text=&quot;请拖放需要压缩的目录或文件到下面输入框中&quot;;z=2;cls=&quot;static&quot; };
staticDst={ dl=1;bottom=118;right=309;left=41;dt=1;top=96;transparent=1;text=&quot;请选择cab文件存储路径&quot;;z=5;cls=&quot;static&quot; };
cmbType={ dr=1;bottom=334;right=377;left=314;text=&quot;LZX&quot;;
items={ &quot;LZX&quot;;&quot;MSZIP&quot; };top=314;z=7;db=1;mode=&quot;dropdown&quot;;edge=1;cls=&quot;combobox&quot; };
staticType={ dr=1;bottom=339;align=&quot;right&quot;;text=&quot;压缩算法:&quot;;left=238;top=317;z=6;db=1;right=309;transparent=1;cls=&quot;static&quot; };
editSrc={ dl=1;bottom=77;right=549;left=41;dt=1;top=48;z=1;edge=1;cls=&quot;edit&quot; };
btnBrowser={ dl=1;bottom=149;right=465;left=349;dt=1;top=116;z=4;text=&quot;浏览...&quot;;cls=&quot;button&quot; };
editLog={ dr=1;dl=1;bottom=289;vscroll=1;right=550;left=38;multiline=1;top=171;dt=1;z=9;db=1;hscroll=1;edge=1;cls=&quot;richedit&quot; }
)

frmCompress.btnCompress.oncommand = function(id,event){
    frmCompress.btnCompress.disabledText = {&#39;\uF254&#39;;&#39;\uF251&#39;;&#39;\uF252&#39;;&#39;\uF253&#39;;&#39;\uF250&#39;}

    thread.invokeAndWait(
        function(frmCompress,path,target,compressType){
            import win.ui;
            import fsys.cab.maker;
            import console;

            frmCompress.editLog.text = &quot;正在计算文件数目......&quot;
            fsys.cab.maker.logger = frmCompress.editLog; 
            ok,err = fsys.cab.maker.compress(path,target,compressType)
            if(ok){
                frmCompress.editLog.appendText(  &#39;\r\n已生成&#39; 
                    , err , &#39;\r\n全部操作已完成\r\n&#39; 
                ); 
            }
            else {
                frmCompress.editLog.appendText( &#39;\r\n&#39; , err );
            }
        },frmCompress,frmCompress.editSrc.text,frmCompress.editDst.text
        /*,frmCompress.cmbType.text*/
    )

    frmCompress.btnCompress.disabledText = null;
} 
import fsys.dlg;
frmCompress.btnBrowser.oncommand = function(id,event){
    frmCompress.editDst.text = fsys.dlg.save( &quot;*.cab|*.cab||&quot;,&quot;请选择cab文件存储目录&quot;,,frmCompress.hwnd)
} 
frmCompress.wndproc = function(hwnd,message,wParam,lParam){
    select(message) {//判断消息类型
        case 0x233/*_WM_DROPFILES*/ { 
            var path = win.getDropFile(wParam )[1];
            if( ..string.endWith(path,&quot;.cab&quot;,true) ){
                frmUnCompress.editSrc.text = path;
                winform.tab.selIndex = 2;
                return;
            }

            frmCompress.editSrc.text = path 
            var tPath =  io.splitpath(frmCompress.editSrc.text)

            if( fsys.isDir(frmCompress.editSrc.text) ){
                frmCompress.editDst.text = tPath.dir + tPath.file + &quot;.cab&quot;
            } 
            else {
                frmCompress.editDst.text = tPath.dir + tPath.name + &quot;.cab&quot;
            }


            frmUnCompress.editSrc.text = frmCompress.editDst.text;
        } 
    }
} 
/*}}*/

/*解压缩工具{{*/
frmUnCompress = winform.tab.add(text=&quot;解压缩&quot;;right=590;bottom=363;acceptfiles=1;parent=...)
frmUnCompress.add(
btnBrowser={cls=&quot;button&quot;;text=&quot;浏览...&quot;;left=406;top=117;right=522;bottom=150;dl=1;dt=1;z=4};
btnUnCompress={cls=&quot;button&quot;;text=&quot;解压缩文件&quot;;font=LOGFONT(name=&#39;FontAwesome&#39;);left=320;top=309;right=480;bottom=349;dl=1;dt=1;z=6};
editDst={cls=&quot;edit&quot;;text=&quot;/&quot;;left=41;top=120;right=381;bottom=147;dl=1;dt=1;edge=1;z=3};
editLog={cls=&quot;richedit&quot;;left=40;top=160;right=566;bottom=301;db=1;dl=1;dr=1;dt=1;edge=1;multiline=1;z=7};
editSrc={cls=&quot;edit&quot;;left=40;top=48;right=566;bottom=77;dl=1;dt=1;edge=1;z=1};
static={cls=&quot;static&quot;;text=&quot;请拖放需要解压的cab文件到下面输入框中&quot;;left=41;top=26;right=309;bottom=48;dl=1;dt=1;transparent=1;z=2};
staticDst={cls=&quot;static&quot;;text=&quot;请选择存储目录&quot;;left=41;top=96;right=309;bottom=118;dl=1;dt=1;transparent=1;z=5}
)

frmUnCompress.onFileInCabinet = function(filename,targetDir,fullTargetName){
    frmUnCompress.editLog.log(&quot;正在解压:&quot;,filename,&#39;\n&#39;);
}

frmUnCompress.btnUnCompress.oncommand = function(id,event){
    frmUnCompress.btnUnCompress.disabledText = {&#39;\uF254&#39;;&#39;\uF251&#39;;&#39;\uF252&#39;;&#39;\uF253&#39;;&#39;\uF250&#39;}

    thread.invokeAndWait(
        function( frmUnCompress,path,target ){ 
            import fsys.cab;
            fsys.cab.onFileInCabinet = function(...){
                frmUnCompress.onFileInCabinet(...)
            };
            fsys.cab.extract(path,target)
            frmCompress.editLog.log(&quot;操作已完成&quot;);
        },frmUnCompress,frmUnCompress.editSrc.text,frmUnCompress.editDst.text 
    )

    frmUnCompress.btnUnCompress.disabledText  = null;
} 

import fsys.dlg.dir;
frmUnCompress.btnBrowser.oncommand = function(id,event){
    frmUnCompress.editDst.text = import fsys.dlg.dir( frmUnCompress.editDst.text,frmUnCompress.hwnd )
}

frmUnCompress.wndproc = function(hwnd,message,wParam,lParam){
    select(message) {//判断消息类型
        case 0x233/*_WM_DROPFILES*/ { 
            var path = win.getDropFile(wParam )[1];
            if( !..string.endWith(path,&quot;.cab&quot;,true) ){
                frmCompress.editSrc.text = path;
                winform.tab.selIndex = 1;
                return;
            }

            frmUnCompress.editSrc.text = path; 
            var tPath =  io.splitpath(frmUnCompress.editSrc.text)
            frmUnCompress.editDst.text = tPath.dir + tPath.name 
        } 
    }
} 

/*}}*/

winform.enableDpiScaling();
winform.show() 
win.loopMessage();

</code></pre>
<a href="https://www.aardio.com/zh-cn/doc/example/File/Compression/CabQuicker.md">Markdown 格式</a>
</body> 
</html>