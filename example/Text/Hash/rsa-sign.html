
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言范例 - RSA 签名'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言范例 - RSA 签名</title> 
<link rel="stylesheet" href="../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio 范例: RSA 签名</h1><pre><code class="aardio language-aardio">//RSA 签名
import win.ui;
/*DSG{{*/
var winform = win.form(text=&quot;RSA签名&quot;;right=797;bottom=561)
winform.add(
btnExportPrivatePkcs1Raw={cls=&quot;button&quot;;text=&quot;导出 PKCS#1 私钥&quot;;left=29;top=196;right=166;bottom=232;dl=1;dt=1;z=5};
btnExportPrivatePkcs8={cls=&quot;button&quot;;text=&quot;导出 PKCS#8 私钥&quot;;left=29;top=87;right=166;bottom=123;dl=1;dt=1;z=4};
btnExportPublicPkcs1Raw={cls=&quot;button&quot;;text=&quot;导出 PKCS#1 公钥&quot;;left=29;top=141;right=166;bottom=177;dl=1;dt=1;z=3};
btnExportPublicX509={cls=&quot;button&quot;;text=&quot;导出 SPKI 公钥&quot;;left=29;top=32;right=166;bottom=68;dl=1;dt=1;z=2};
btnImportKey={cls=&quot;button&quot;;text=&quot;导入公钥或私钥(自动识别)&quot;;left=418;top=257;right=768;bottom=293;db=1;dr=1;z=6};
btnSign={cls=&quot;button&quot;;text=&quot;签名(使用私钥)&quot;;left=435;top=509;right=569;bottom=545;db=1;dr=1;z=8};
btnVerify={cls=&quot;button&quot;;text=&quot;验证(使用公钥)&quot;;left=586;top=509;right=720;bottom=545;db=1;dr=1;z=9};
editKey={cls=&quot;richedit&quot;;left=190;top=22;right=770;bottom=252;db=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(name=&#39;新宋体&#39;);hscroll=1;multiline=1;vscroll=1;z=1};
editSign={cls=&quot;edit&quot;;left=190;top=302;right=770;bottom=368;db=1;dl=1;dr=1;edge=1;hscroll=1;multiline=1;z=10};
editText={cls=&quot;richedit&quot;;text=&quot;测试数据( UTF-8 编码)&quot;;left=29;top=376;right=770;bottom=504;db=1;dl=1;dr=1;edge=1;hscroll=1;multiline=1;vscroll=1;z=7};
static={cls=&quot;static&quot;;text=&quot;签名:&quot;;left=78;top=316;right=174;bottom=337;align=&quot;right&quot;;db=1;dl=1;transparent=1;z=11}
)
/*}}*/

import crypt.rsa;
var rsa = crypt.rsa();
rsa.genSignatureKey(); 

winform.btnExportPublicX509.oncommand = function(id,event){
    //导出通用的 SPKI (Subject Public Key Info) 格式公钥
    winform.editKey.text = rsa.exportPublicKeyX509ToPem();
}

winform.btnExportPublicPkcs1Raw.oncommand = function(id,event){
    winform.editKey.text = rsa.exportPublicKeyPkcs1RawToPem();
}

winform.btnExportPrivatePkcs8.oncommand = function(id,event){
    winform.editKey.text = rsa.exportPrivateKeyPkcs8ToPem();
}

winform.btnExportPrivatePkcs1Raw.oncommand = function(id,event){
    winform.editKey.text = rsa.exportPrivateKeyPkcs1RawToPem(); 
}

winform.btnImportKey.oncommand = function(id,event){
    var header = rsa.importPemKey(winform.editKey.text);
    if(header) {
        winform.msgbox(&quot;已导入：&quot; + header); 
    }
    else winform.msgboxErr(&quot;错误的密钥格式&quot;)
}

winform.btnSign.oncommand = function(id,event){

    //SHA256withRSA
    rsa.createHashBySha256(winform.editText.text); 
    var sign =  rsa.signToBase64();  

    if(!sign) return winform.msgboxErr(&quot;签名失败,请检查是否导入了正确的私钥&quot;); 
    winform.editSign.text = sign;   

}

winform.btnVerify.oncommand = function(id,event){

    //SHA256withRSA
    rsa.createHashBySha256(winform.editText.text);
    if( !rsa.verifyFromBase64( winform.editSign.text ) ){ 
        winform.msgboxErr(&quot;签名是错误的,数据已被篡改,或未导入正确的公钥&quot;);
    } 
    else {
        winform.msgbox(&quot;签名是正确的,数据未被篡改&quot;);
    } 
}

winform.show() 
win.loopMessage();
</code></pre>
<a href="https://www.aardio.com/zh-cn/doc/example/Text/Hash/rsa-sign.md">Markdown 格式</a>
</body> 
</html>