
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言范例 - RPC 服务端 - 支持任意本地网页调用 aardio 函数'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言范例 - RPC 服务端 - 支持任意本地网页调用 aardio 函数</title> 
<link rel="stylesheet" href="../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio 范例: RPC 服务端 - 支持任意本地网页调用 aardio 函数</h1><pre><code class="aardio language-aardio">//external 服务器
import win.ui;
/*DSG{{*/
var winform = win.form(text=&quot;RPC 服务端 - 支持任意本地网页调用 aardio 函数&quot;;right=759;bottom=469)
winform.add(
edit={cls=&quot;richedit&quot;;left=23;top=24;right=730;bottom=446;edge=1;hscroll=1;multiline=1;vscroll=1;z=1}
)
/*}}*/

import web.rpc.externalServer;
var externalServer = web.rpc.externalServer();

//只有下面指定的函数网页才能调用
externalServer.external = {
    test = function(...){
        winform.edit.print(&quot;external.test 被调用&quot;,...)
    }
    tag = function(strs,...){
        var args = {...}
        for(i=#args;1;-1){
            table.insert(strs,args[i],i+1);
        }

        strs = string.join(strs);
        return strs;
    }
}

//可选用下面的方法限定只有下面允许的外部域名才能访问 aardio 函数
externalServer.accessControlAllowOrigin = {
    [&quot;https://d.aardio.com&quot;] = true //指定准确前缀，域名后不要加斜杆
}

//如果不指定端口号，则默认动态分配空闲的端口号
//如果不指定具体的端口号，则只有 externalServer 提供的网页才能自 aardio.js 自动获取到端口号
winform.edit.print( &quot;启动服务器：&quot;,externalServer.start())
winform.edit.print( &quot;请在网页中引入：&quot;,externalServer.getUrl(&quot;aardio.js&quot;))

externalServer.httpHandler[&quot;/test.html&quot;] = function(response,request){
    var html = /**
    &lt;!doctype html&gt;
    &lt;html&gt;
    &lt;head&gt;
        &lt;meta charset=&quot;utf-8&quot;&gt;
        &lt;meta http-equiv=&quot;X-UA-Compatible&quot; content=&quot;IE=edge&quot; /&gt;
        &lt;script src=&quot;&lt;?=owner.rpcUrl?&gt;&quot;&gt;&lt;/script&gt;
        &lt;script&gt;
        /*
        也可以在前端源码中用 npm i aardio 安装 aardio.js 。
        externalServer 提供的 aardio.js 自动获取 RPC 端口并初始化 RPC 方法。
        如果在创建 web.rpc.externalServer 以后调用标准库中的 nodeJs 创建 Node.js  进程，
        也可以自动获取端口号并在连接成功前创建远程函数。

        远程服务器上的网页可通过 URL 参数获取 RPC 端口并初始化 RPC 方法。
        也可以调用 aardio.open( 端口号 ) 连接指定的端口。
        可参考：https://d.aardio.com/ajs/demo.html
        */

        //调用 aardio 函数。
        aardio.test(&quot;这是网页调用 aardio 的参数&quot;,1122);

        //用 aardio 解析模板字符串
        let $ = aardio.tag;
        $`abc${123}ddd${456}`.then( v=&gt; alert(&quot;模板字符串:&quot;+v)  )

        &lt;/script&gt;
    &lt;/head&gt;
    &lt;body&gt;&lt;?=owner.rpcUrl?&gt;&lt;/body&gt;&lt;/html&gt;
    **/
    response.write( string.loadcode(html,{rpcUrl=externalServer.getUrl(&quot;aardio.js&quot;)}) );    
}

import process;
process.openUrl(externalServer.getUrl(&quot;/test.html&quot;))

/*
如果传入小写 http: 或 https: 开头的网址，
则返回了附加 rpcServerPort 与 rpcAasdl 参数的网址，
aardio.js 识别这些参数后自动获取 RPC 端口并初始化 RPC 方法。

未传以上参数则网页中必须调用 aardio.open 连接指定的端口。 
*/
var rpcUrl = externalServer.getUrl(&quot;https://d.aardio.com/ajs/demo.html&quot;);
process.openUrl( rpcUrl );

//上面 demo.html 的源码如下：
/**
&lt;!doctype html&gt;
&lt;html&gt;&lt;head&gt;
    &lt;meta charset=&quot;utf-8&quot;&gt;
    &lt;script src=&quot;https://d.aardio.com/ajs/aardio.js&quot;&gt;&lt;/script&gt;
    &lt;script type=&quot;text/javascript&quot;&gt;
        (async () =&gt; {
            /*
            aardio.js 可自动从网址参数 rpcServerPort，rpcAasdl 获取并连接端口与所有远程函数。 
            如果未指定参数，则应调用 aardio.open( RPC端口号 ) 打开 RPC 服务器。 
            aardio.open 返回 Promise 对象。 

            连接成功前只能使用 aardio.xcall 调用本地函数。 
            */
            //await aardio.open(8082);

            await aardio.test(&quot;这是网页调用 aardio 的参数&quot;, 1122);

            let $ = aardio.tag;
            $`abc${123}ddd${456}`.then(v =&gt; alert(&quot;模板字符串:&quot; + v));
        })()
    &lt;/script&gt;
&lt;/head&gt;&lt;body&gt;&lt;/body&gt;&lt;/html&gt;
**/

winform.show();
win.loopMessage();
</code></pre>
<a href="https://www.aardio.com/zh-cn/doc/example/Web/RPC/externalServer.md">Markdown 格式</a>
</body> 
</html>