
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言范例 - AI 聊天助手'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言范例 - AI 聊天助手</title> 
<link rel="stylesheet" href="../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio 范例: AI 聊天助手</h1><pre><code class="aardio language-aardio">import win.ui;
import fonts.fontAwesome;
/*DSG{{*/
var winform = win.form(text=&quot;aardio - AI 聊天助手&quot;;right=759;bottom=607)
winform.add(
btnClear={cls=&quot;plus&quot;;text=&quot;清除&quot;;left=589;top=572;right=655;bottom=602;align=&quot;left&quot;;color=3947580;db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align=&quot;left&quot;;font=LOGFONT(h=-13;name=&#39;FontAwesome&#39;);padding={left=8}};iconText=&#39;\uF014&#39;;notify=1;textPadding={left=25};z=6};
btnCopy={cls=&quot;plus&quot;;text=&quot;复制&quot;;left=202;top=572;right=269;bottom=602;align=&quot;left&quot;;color=3947580;db=1;disabled=1;dr=1;font=LOGFONT(h=-13);iconStyle={align=&quot;left&quot;;font=LOGFONT(h=-13;name=&#39;FontAwesome&#39;);padding={left=8}};iconText=&#39;\uF0C5&#39;;notify=1;textPadding={left=25};z=11};
btnSend={cls=&quot;plus&quot;;text=&quot;问 AI&quot;;left=660;top=572;right=732;bottom=602;align=&quot;left&quot;;color=3947580;db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align=&quot;left&quot;;font=LOGFONT(h=-13;name=&#39;FontAwesome&#39;);padding={left=8}};iconText=&#39;\uF0AA&#39;;notify=1;textPadding={left=25};z=5};
btnSetting={cls=&quot;plus&quot;;text=&quot;设置&quot;;left=513;top=572;right=580;bottom=602;align=&quot;left&quot;;color=3947580;db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align=&quot;left&quot;;font=LOGFONT(h=-13;name=&#39;FontAwesome&#39;);padding={left=8}};iconText=&#39;\uF013&#39;;notify=1;textPadding={left=25};z=7};
chkFix={cls=&quot;plus&quot;;text=&quot;更正&quot;;left=276;top=572;right=336;bottom=603;align=&quot;left&quot;;db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align=&quot;left&quot;;font=LOGFONT(h=-14;name=&#39;FontAwesome&#39;)};iconText=&#39;\uF0C8 &#39;;notify=1;textPadding={left=24};z=12};
editMaxTokens={cls=&quot;edit&quot;;left=427;top=577;right=470;bottom=600;align=&quot;right&quot;;db=1;dr=1;edge=1;z=9};
editPrompt={cls=&quot;richedit&quot;;left=11;top=452;right=754;bottom=567;db=1;dl=1;dr=1;edge=1;hscroll=1;multiline=1;vscroll=1;z=2};
promptTool={cls=&quot;syslink&quot;;text=&quot;提示词生成器 / AI 搜索&quot;;left=20;top=577;right=163;bottom=600;center=1;db=1;dl=1;notify=1;z=4};
spinMaxTokens={cls=&quot;spin&quot;;left=471;top=578;right=491;bottom=600;db=1;dr=1;z=8};
splitter={cls=&quot;splitter&quot;;left=8;top=453;right=751;bottom=458;db=1;dl=1;dr=1;frame=1;horz=1;z=3};
static={cls=&quot;static&quot;;text=&quot;回复长度：&quot;;left=352;top=575;right=418;bottom=598;align=&quot;right&quot;;center=1;db=1;dr=1;transparent=1;z=10};
wndBrowser={cls=&quot;custom&quot;;text=&quot;自定义控件&quot;;left=8;top=5;right=751;bottom=447;db=1;dl=1;dr=1;dt=1;z=1}
)
/*}}*/

//创建显示聊天消虑的 Web 浏览器窗口
import web.form.chat;
var wb = web.form.chat(winform.wndBrowser);

import fsys.table;
var config = fsys.table(io.appData(&quot;aardio/ide/aiChat/~&quot;))

var systemPrompt = /****************
## 角色

你是 aardio 编程助手，擅长  aardio 编程。

## 任务

你会解答  aardio 编程问题，并且帮助用户生成或改进 aardio 代码。

## aardio 语法要求

- 使用 `{}` 标记语句块。
- 使用 `{}` 构造器创建表（对象或数组），例如 `var arr = {1,2,3}`。        
- 数组起始索引为 1 。 
- 只能使用 var 声明局部变量。
- 使用 `++` 连接字符串。 
- 函数默认参数只能定义为布尔值、字符串、数值之一。
- 双引号包含的是原始字符串，不处理转义符。
- aardio 默认使用模式匹配语法代替正则表达式。
    - 模式匹配使用尖括号`&lt;&gt;`包含非捕获组。
    - 非捕获组内部不能包含捕获组。
    - 只能对单个字符或非捕获组可以使用模式运算符（例如量词），不能对捕获组使用模式运算符。
    - 用双引号包含模式串，模式转义符应当写为单个反斜杆而不是双反斜杆，例如 `&quot;\d&quot;` 不能写为  `&quot;\\d&quot;`。

## for 循环语句格式

```aardio
for(i = initialValue;finalValue;incrementValue){
    // code block to be executed
}
```

- aardio 使用基于数值范围的 for 循环，全部循环参数都必须是数值表达式，没有`条件（condition-expression）`与`迭代（iteration-expression）`部分。
- 循环数值范围自 initialValue 开始到 finalValue 结束，finalValue 必须是确定的数值。
- 循环增量 incrementValue 必须是数值。

示例：

```aardio

//声明数组
var arr = {1,2,3}

//循环遍历数组，#arr 取数组长度
for(i=1;#arr;1){
    print(arr[i]);//循环输出所有数组成员
}
```

## import 语句要求

- aardio 中除 raw,string,table,math,io,time,thread 等无需导入的内置库以外，其他所有库（标准库或扩展库）都必须先用 import 语句导入后才能使用。
- 如果代码中用到了 `win.form` 则必须使用 `import win.ui` 导入 win.form 窗口类 。
- 如果代码中需要操作 JSON，则必须使用 `import web.json` 导入 web.json 库。 

## 窗口程序示例

```aardio
import win.ui;
/*DSG{{*/
var winform = win.form({text=&quot;第一个 aardio 窗口程序&quot;})
winform.add({
button={cls=&quot;button&quot;;text=&quot;点这里&quot;;note=&quot;这是一个很酷的按钮&quot;;left=435;top=395;right=680;bottom=450;color=14120960;z=2};
edit={cls=&quot;edit&quot;;left=18;top=16;right=741;bottom=361;edge=1;multiline=1;z=1}
})
/*}}*/

//按钮回调事件
winform.button.oncommand = function(){

    //修改控件属性
    winform.edit.text = &quot;Hello, World!&quot;;

    //输出字符串或对象，自动添加换行
    winform.edit.print(&quot; 你好！&quot;);

    //创建工作线程
    thread.invoke( 
        function(winform){

            //每个线程使用隔离的变量环境，线程使用的库必须单独导入
            import web.rest.jsonLiteClient;

            //创建 HTTP 客户端，响应数据格式为 JSON，提交数据格式为 Url Encoded。如果提交与响应格式都是 JSON 请改用 web.rest.jsonClient 。
            var http = web.rest.jsonLiteClient();

            //声明 HTTP API 对象。
            var delivery = http.api(&quot;https://api.pi.delivery/v1/pi&quot;); 

            //使用 &quot;GET&quot; 方法发送请求并查询圆周率，参数指定一个表对象（table），单个表参数外层的 {} 也可以省略不写
            var ret = delivery.get({
                start=1, //从第 1 位开始
                numberOfDigits=100 //返回 100 位圆周率
            })

            //在工作线程中调用界面控件的属性与方法会自动转发到界面线程执行。
            winform.edit.print(&quot;圆周率：&quot; ,&quot;3.&quot;+ ret.content);

        },winform //线程函数都是纯函数，外部线程的对象必须通过参数传入线程
    )
}

//显示窗口
winform.show();

//启动界面线程的消息循环
win.loopMessage();
```

## 调用 web.view 的网页界面示例

```aardio
import win.ui;
var winform = win.form(text=&quot;WebView2&quot;); //创建窗口

import web.view;
var wb = web.view(winform);//在宿主窗口 winform 内创建 WebView2 浏览器窗口

//导出 aardio 对象到 JavaScript 。    
wb.external = {
    getNativeObject = function(){ 
        return {prop1=123;prop2=&quot;NativeObject value&quot;}
    };
}

//导出 aardio 函数为 JavaScript 全局函数。
wb.export({
    getJsonObject = function(){
        return {prop1=123;prop2=&quot;JsonObject value&quot;}
    } 
})

//写入网页
wb.html = /**
&lt;!doctype html&gt;
&lt;html&gt;&lt;head&gt;
&lt;script&gt; 
(async()=&gt;{

    //在 JavaScript 内调用 wb.externa 导出的 aardio 对象。
    var nativeObject = await aardio.getNativeObject(); //返回基于 COM 接口的对象。

    //nativeObject 的属性与方法都是 Promise 
    var prop2 = await nativeObject.prop2; 

    //调用 wb.export 导出的 aardio 函数，参数与返回值通过 JSON 转换
    var jsonObject = await getJsonObject()

    // jsonObject 是纯 JavaScript 对象
    alert( jsonObject.prop2 )
})()
&lt;/script&gt;
**/

winform.show();
win.loopMessage();
****************/

//清除上下文
var resetMessages = function(){

    wb.clear();

    var p = systemPrompt;

    if(config.quickRef){
        p = p + &#39;\r\n\r\n---\r\n\r\n&#39; + string.load(&quot;~\doc\guide\language\syntax-quick-ref.md&quot;);
    }

    //输入系统提示词
    wb.system(  p );
}

resetMessages();

winform.btnClear.oncommand = function(id,event){
    resetMessages();//清除聊天上下文
}


//响应按键事件，输入用户提示词
winform.btnSend.oncommand = function(id,event){
    var prompt = winform.editPrompt.text;
    if(!#prompt){
        winform.msgboxErr(&quot;请在上面输入问题。&quot;); 
        winform.editPrompt.setFocus();
        return;
    }

    //按钮显示等待动画
    winform.btnSend.disabledText = {&#39;\uF254&#39;;&#39;\uF251&#39;;&#39;\uF252&#39;;&#39;\uF253&#39;;&#39;\uF250&#39;}
    winform.btnClear.disabled = true;
    winform.btnCopy.disabled = true;
    winform.chkFix.disabled = true;

    wb.limit = config.msgLimit;

    var assistantMsg = wb.lastAssistantMessage();
    if(assistantMsg &amp;&amp; winform.chkFix.checked){
        //Few-shot Learning
        assistantMsg.content = ide.aifix.markdown(assistantMsg.content,true);
    }

    var knowledge = &quot;&quot;
    prompt = string.replace(prompt,&quot;(https?\://&lt;www\.&gt;?aardio\.com/zh\-cn/doc/\S+)\.&lt;html&gt;|&lt;md&gt;&quot;,
        function(url){

            url = url + &quot;.md&quot;;
            wb.showLoading(&quot;正在读取：&quot;+url)

            var md = inet.http.get(url);
            if(md){
                md = &#39;\r\n\r\n用户输入的参考网址：&#39; + url 
                    +  &#39;\r\n\r\n下面是自该网址获取的 Markdown 格式文档或范例：&#39;
                    +  &#39;\r\n\r\n&#39; + md +&#39;\r\n\r\n------------------------\r\n\r\n&#39;

                knowledge = knowledge + md;

            }
        });

    if(#knowledge){
        wb.system(knowledge)
    } 

    //输入 AI 提示词
    wb.prompt( prompt );
    winform.editPrompt.text = &quot;&quot;;

    config.maxTokens = winform.spinMaxTokens.pos;

    //创建多线程向服务端发送请求
    thread.invoke( 
        function(wb,config){

            for(k,v in config){ 
                if(v==&quot;&quot;)config[k] = null;
            } 

            config = table.mix(config,{
                /*
                下面的 key 24 小时后失效，
                DeepSeek 的 key 充 10 元估计能用上一年，所以请自己申请一个好吗？！
                */
                key = &quot;sk-2f729981180a45e3b28971e9a5c68c72&quot;;
                url = &quot;https://api.deepseek.com/v1&quot;;
                model = &quot;deepseek-chat&quot;;
                temperature = 0.1;
            });

            if(config.maxTokens&gt;1024 &amp;&amp; (config.key === &quot;sk-2f729981180a45e3b28971e9a5c68c72&quot; ) ){
                wb.errorMessage(`回复长度超过 1024 时，必须更改为您自己的 API 密钥 。&lt;a href=&quot;https://platform.deepseek.com&quot;&gt;点这里获取密钥&lt;/a&gt;，&amp;nbsp;&lt;a href=&quot;javascript:void(0)&quot; onclick=&quot;javascript:external.updateApiKey()&quot;&gt;点这里设置新密钥&lt;/a&gt;。`);
                return;
            }

            //导入调用 HTTP 接口的 REST 客户端
            import web.rest.aiChat;
            var client = web.rest.aiChat(config);

            var ok,err = client.messages(wb.chatMessage,function(deltaText){
                wb.assistant(deltaText);
            } );

            if(err){
                //获取错误对象（解析 JSON 格式的错误信息）
                var errObject = client.lastResponseError()
                if(errObject[[&quot;error&quot;]][[&quot;type&quot;]] == &quot;authentication_error&quot; ){
                    wb.errorMessage(`API 密钥错误！&lt;a href=&quot;https://platform.deepseek.com&quot;&gt;点这里获取密钥&lt;/a&gt;，&amp;nbsp;&lt;a href=&quot;javascript:void(0)&quot; onclick=&quot;javascript:external.updateApiKey()&quot;&gt;点这里设置新密钥&lt;/a&gt;`)
                }
                else {
                    wb.errorMessage(err)
                }
            }  

        },wb,config//将参数传入线程
    )
}

//在 AI 回复结束后回调此函数
wb.onWriteEnd = function(){
    winform.btnSend.disabledText = null;//关闭等待动画
    winform.btnClear.disabled = false;
    winform.btnCopy.disabled = false;
    winform.chkFix.disabled = false;
    winform.editPrompt.setFocus();
}

//在 AI 回复结束以前回调此函数，自动修正 aardio 代码块中的常见幻觉错误
import ide.aifix;
wb.beforerWriteEnd = function(markdown){
    if(winform.chkFix.checked) return ide.aifix.markdown(markdown,true);
    return markdown;
}

//导出 aardio 函数到网页 JavaScript 中。
wb.external = {
    updateApiKey = function(){
        winform.btnSetting.oncommand();
    } 
}

import key;
import win.clip;
winform.btnCopy.oncommand = function(id,event){
    var md = wb.lastMarkdown();
    if(!#md) return winform.msgboxErr(&quot;消息为空。&quot;);

    if(key.getState(&quot;CTRL&quot;)){

        var found;
        for indent,_,code in string.gmatch(md,&quot;!\N([ \t]*)(```+)&lt;aardio&gt;(.+?)!\N\s*\2![^`\S]&quot;) { 

            if(#indent){ 
                text = string.replace(text,&quot;\n+&quot;+indent,&#39;\n&#39;);
            }   

            if(winform.chkFix.checked) code = ide.aifix(code,true);
            win.clip.write( code );
            found = true;
        }

        if(!found){
            return winform.msgboxErr(&quot;没有找到代码块。&quot;);   
        } 
    }
    else{
        if(winform.chkFix.checked) md = ide.aifix.markdown(md,true);
        win.clip.write( md )
    }

    winform.btnCopy.disabledText = {&#39;\uF254&#39;;&#39;\uF251&#39;;&#39;\uF252&#39;;&#39;\uF253&#39;;&#39;\uF250&#39;;text=&#39;&#39;} 
    thread.delay(600);
    winform.btnCopy.disabledText = null;
}

//设置接口地址与 API 令牌的窗口
winform.btnSetting.oncommand = function(id,event){
    var frmSetting = win.form(text=&quot;aardio - 设置 AI 聊天助手&quot;;right=684;bottom=363;border=&quot;dialog frame&quot;;exmode=&quot;none&quot;;max=false;min=false;mode=&quot;popup&quot;)
    frmSetting.add(
        btnAdd={cls=&quot;plus&quot;;left=18;top=235;right=52;bottom=265;align=&quot;left&quot;;color=3947580;font=LOGFONT(h=-13);iconStyle={align=&quot;left&quot;;font=LOGFONT(h=-13;name=&#39;FontAwesome&#39;);padding={left=8}};iconText=&#39;\uF067&#39;;notify=1;textPadding={left=25};z=12};
        btnEdit={cls=&quot;plus&quot;;left=93;top=235;right=127;bottom=265;align=&quot;left&quot;;color=3947580;font=LOGFONT(h=-13);iconStyle={align=&quot;left&quot;;font=LOGFONT(h=-13;name=&#39;FontAwesome&#39;);padding={left=8}};iconText=&#39;\uF044&#39;;notify=1;textPadding={left=25};z=14};
        btnRemove={cls=&quot;plus&quot;;left=56;top=235;right=90;bottom=265;align=&quot;left&quot;;color=3947580;font=LOGFONT(h=-13);iconStyle={align=&quot;left&quot;;font=LOGFONT(h=-13;name=&#39;FontAwesome&#39;);padding={left=8}};iconText=&#39;\uF1F8&#39;;notify=1;textPadding={left=25};z=13};
        btnSave={cls=&quot;button&quot;;text=&quot;更新配置&quot;;left=458;top=298;right=628;bottom=343;z=5};
        chkQuickRef={cls=&quot;checkbox&quot;;text=&quot;新建会话自动发送语法速览（约 8k tokens）&quot;;left=18;top=287;right=272;bottom=309;z=15};
        editApiKey={cls=&quot;edit&quot;;left=284;top=104;right=662;bottom=131;edge=1;password=1;z=2};
        editApiUrl={cls=&quot;combobox&quot;;left=284;top=31;right=662;bottom=57;edge=1;items={};mode=&quot;dropdown&quot;;z=20};
        editModel={cls=&quot;edit&quot;;left=284;top=68;right=662;bottom=95;edge=1;z=7};
        editProxy={cls=&quot;edit&quot;;left=283;top=223;right=661;bottom=250;edge=1;z=21};
        groupbox={cls=&quot;groupbox&quot;;text=&quot;选择当前配置：&quot;;left=9;top=6;right=675;bottom=270;edge=1;z=1};
        lbMsgLimit={cls=&quot;static&quot;;left=390;top=315;right=418;bottom=335;transparent=1;z=18};
        lbTemperature={cls=&quot;static&quot;;left=633;top=140;right=659;bottom=160;transparent=1;z=19};
        lstConfig={cls=&quot;listbox&quot;;left=18;top=34;right=187;bottom=235;edge=1;hscroll=1;items={};vscroll=1;z=11};
        static={cls=&quot;static&quot;;text=&quot;模型 ID：&quot;;left=196;top=72;right=279;bottom=93;align=&quot;right&quot;;transparent=1;z=3};
        static2={cls=&quot;static&quot;;text=&quot;API key：&quot;;left=196;top=107;right=279;bottom=128;align=&quot;right&quot;;transparent=1;z=4};
        static3={cls=&quot;syslink&quot;;text=&#39;推荐使用 Claude 3.5 Sonnet 或 &lt;a href=&quot;https://platform.deepseek.com&quot;&gt;DeepSeek&lt;/a&gt; 。\n模型 ID 前加 @ 使用 Anthropic 接口，否则使用 OpenAI 接口。&#39;;left=283;top=176;right=653;bottom=224;transparent=1;z=6};
        static4={cls=&quot;static&quot;;text=&quot;接口地址：&quot;;left=196;top=38;right=279;bottom=59;align=&quot;right&quot;;transparent=1;z=8};
        static5={cls=&quot;static&quot;;text=&quot;temperature：&quot;;left=196;top=141;right=279;bottom=162;align=&quot;right&quot;;transparent=1;z=10};
        static6={cls=&quot;static&quot;;text=&quot;上下文轮数：&quot;;left=0;top=321;right=95;bottom=353;align=&quot;right&quot;;transparent=1;z=17};
        static7={cls=&quot;static&quot;;text=&quot;代理服务器：&quot;;left=196;top=227;right=279;bottom=248;align=&quot;right&quot;;transparent=1;z=22};
        tbMsgLimit={cls=&quot;trackbar&quot;;left=99;top=313;right=382;bottom=343;max=100;min=0;z=16};
        tbTemperature={cls=&quot;trackbar&quot;;left=283;top=137;right=633;bottom=167;max=100;min=0;z=9}
    )

    frmSetting.editProxy.setCueBannerText(&quot;socks=127.0.0.1:1081&quot;)

    frmSetting.editApiUrl.items = {
        &quot;https://api.deepseek.com/v1&quot;,
        &quot;https://api.anthropic.com/v1&quot;,
        &quot;https://generativelanguage.googleapis.com/v1beta&quot;,
        &quot;https://api.x.ai/v1&quot;,
        &quot;https://api.openai.com/v1&quot;
    }

    frmSetting.editApiUrl.onListChange = function(){ 
        var url = frmSetting.editApiUrl.selText;
        if(url==&quot;https://api.deepseek.com/v1&quot;){
            frmSetting.editModel.text = &quot;deepseek-chat&quot;
        }
        elseif(url==&quot;https://api.anthropic.com/v1&quot;){
            frmSetting.editModel.text = &quot;@claude-3-5-sonnet-latest&quot;
        }
        elseif(url==&quot;https://generativelanguage.googleapis.com/v1beta&quot;){
            frmSetting.editModel.text = &quot;gemini-2.0-flash-exp&quot;
        }
        elseif(url==&quot;https://api.x.ai/v1&quot;){
            frmSetting.editModel.text = &quot;grok-2-1212&quot;
        }
        elseif(url==&quot;https://api.openai.com/v1&quot;){
            frmSetting.editModel.text = &quot;chatgpt-4o-latest&quot;
        }
    }

    frmSetting.tbMsgLimit.setRange(3,100);
    frmSetting.tbTemperature.setRange(0,10);
    frmSetting.tbTemperature.oncommand = function(id,event,pos){

        var pos = frmSetting.tbTemperature.pos; 
        frmSetting.tbTemperature.tooltip = pos / 10; 
        frmSetting.lbTemperature.text = pos / 10; 
    }

    frmSetting.tbMsgLimit.oncommand = function(id,event,pos){

        frmSetting.lbMsgLimit.text = frmSetting.tbMsgLimit.pos;;  
    }

    import win.ui.listEdit;
    var listEdit = win.ui.listEdit(frmSetting.lstConfig);
    listEdit.editBox.setCueBannerText(&quot;请输入配置名&quot;,true);

    if(!#config.itemNames) {
        config.itemNames = {&quot;默认&quot;}
        config.itemData = {{
            url = config.url || &quot;https://api.deepseek.com/v1&quot;;
            key = config.key;
            model = #config.model ? config.model : &quot;deepseek-chat&quot;;
            temperature = config.temperature;       
        }};
    } 

    frmSetting.lstConfig.onSelChange = function(){
        var selIndex = frmSetting.lstConfig.selIndex; 

        if(config.selItem &amp;&amp; config.selItem != selIndex){
            //保存上一个配置
            var configItem = {
                url = frmSetting.editApiUrl.text;
                key = frmSetting.editApiKey.text;
                model = frmSetting.editModel.text;
                temperature = frmSetting.tbTemperature.pos / 10;
                msgLimit = frmSetting.tbMsgLimit.pos;
                proxy = string.trim(frmSetting.editProxy.text);
            }  

            config.itemData[config.selItem] = configItem;       
        } 

        //加载下一个配置
        var selIndex = frmSetting.lstConfig.selIndex; 
        var configItem = config.itemData[selIndex] || {};

        frmSetting.editApiUrl.text = configItem.url;
        frmSetting.editApiKey.text = configItem.key;
        frmSetting.editProxy.text = configItem.proxy;

        if(configItem.temperature===null) configItem.temperature = 0.1; 
        frmSetting.tbTemperature.pos = configItem.temperature * 10;
        frmSetting.tbTemperature.tooltip = configItem.temperature; 
        frmSetting.lbTemperature.text = configItem.temperature; 

        frmSetting.tbMsgLimit.pos = configItem.msgLimit || 15;
        frmSetting.lbMsgLimit.text = configItem.msgLimit || 15; 

        frmSetting.editModel.text = configItem.model; 

        config.proxy = null;
        table.assign(config,configItem);

        config.itemData[selIndex] = configItem;
        config.itemNames = frmSetting.lstConfig.items; 
        config.itemData = table.slice(config.itemData,1,#config.itemNames);
        config.selItem = selIndex; 

        config.save();  
    }

    frmSetting.lstConfig.items = config.itemNames;
    frmSetting.lstConfig.selIndex = config.selItem || 1;
    frmSetting.lstConfig.onSelChange();
    frmSetting.chkQuickRef.checked = config.quickRef;

    frmSetting.editModel.onChange = function(){ 
         if(string.find(owner.text,&quot;&lt;grok\-beta&gt;&quot;)){
            frmSetting.chkQuickRef.checked = true;
         }
    }

    //保存并更新配置
    import inet.url;
    frmSetting.btnSave.oncommand = function(id,event){

        var configItem = {
            url = frmSetting.editApiUrl.text;
            key = frmSetting.editApiKey.text;
            model = frmSetting.editModel.text;
            temperature = frmSetting.tbTemperature.pos / 10;
            msgLimit = frmSetting.tbMsgLimit.pos;
            proxy = string.trim(frmSetting.editProxy.text);
        }

        if(!#configItem.proxy){
            configItem.proxy = null;
        }

        var tUrl = inet.url.split(configItem.url); 
        if(tUrl[[&quot;host&quot;]]==&quot;api.anthropic.com&quot;){
            if(configItem.model[1]!=&#39;@&#39;#){
                configItem.model = &#39;@&#39; + configItem.model;
            }
        }
        elseif(tUrl[[&quot;host&quot;]]==&quot;generativelanguage.googleapis.com&quot;){
            configItem.url = &quot;https://generativelanguage.googleapis.com/v1beta&quot;
        }

        var selIndex = frmSetting.lstConfig.selIndex;
        config.selItem = selIndex; 
        config.itemData[selIndex] = configItem;

        config.proxy = null;
        table.assign(config,configItem);  

        config.quickRef = frmSetting.chkQuickRef.checked;
        config.save();

        frmSetting.endModal();

        if(!wb.started()){
            resetMessages();
        }

        thread.delay(100)
        winform.editPrompt.setFocus();
    }

    frmSetting.btnEdit.oncommand = function(id,event){
        listEdit.beginEdit();
    }

    frmSetting.btnAdd.oncommand = function(id,event){
        listEdit.beginEdit(0);  
    }

    frmSetting.btnRemove.oncommand = function(id,event){
        if(frmSetting.lstConfig.count==1){
            return frmSetting.msgboxErr(&quot;只有一个配置方案时不允许删除！&quot;);
        }

        var selIndex = frmSetting.lstConfig.selIndex;
        frmSetting.lstConfig.delete(selIndex)

        frmSetting.lstConfig.selIndex = selIndex&lt;=frmSetting.lstConfig.count ? selIndex : selIndex -1;
        frmSetting.lstConfig.onSelChange() 
    }   

    listEdit.onEditChanged = function(newText,selIndex){
        config.itemNames = frmSetting.lstConfig.items; 

        frmSetting.lstConfig.selIndex = selIndex;
        frmSetting.lstConfig.onSelChange();

        config.save();  
    }

    frmSetting.btnAdd.skin({
        color={
            active=0xFF00FF00;
            default=0xFF3C3C3C;
            disabled=0xFF6D6D6D;
            hover=0xFFFF0000        
        }
    })

    frmSetting.btnRemove.skin({
        color={
            active=0xFF00FF00;
            default=0xFF3C3C3C;
            disabled=0xFF6D6D6D;
            hover=0xFFFF0000        
        }
    })

    frmSetting.btnEdit.skin({
        color={
            active=0xFF00FF00;
            default=0xFF3C3C3C;
            disabled=0xFF6D6D6D;
            hover=0xFFFF0000        
        }
    })

    frmSetting.doModal(winform);
}

winform.chkFix.checked = true;
winform.chkFix.oncommand = function(id,event){
    var md = wb.getMarkdown();
    if(owner.checked){
        md = ide.aifix.markdown(md,true)
    }

    wb.write(md);
}

wb.write(&quot;
- 打开此工具可自动获取编辑器选中代码块，并生成提示词。
- 在编辑器选中代码块，按 `Shift + F1` 可打开此工具。注意不要在放开 `F1` 前放开 `Shift` 键。
- 按住 `Ctrl`键点 `复制` 按钮可复制 AI 最后一次输出的代码块。
- 请注意查看下面提示词输入框的右键菜单。
- 本工具支持联网自动读取 aardio 文档，复制 aardio 在线文档链接会自动插入提示词。
- [点这里开通 VIP 专属技术支持服务](https://aardio.com/vip)
&quot;)

//获取 IDE 中当前选中的代码
import ide;
var codeEditor = ide.getActiveCodeEditor();
if(codeEditor){
    var selText = codeEditor.selText;
    if(selText){
        selText = &#39;解释代码：\r\n\r\n``````aardio\r\n&#39; + selText + &#39;\r\n``````&#39;;
        winform.editPrompt.text = string.crlf(selText);
    }
}

//默认设置输入框焦点
winform.editPrompt.setFocus();

import win.clip.viewer;
var clipViewer = win.clip.viewer(winform)
clipViewer.onDrawClipboard=function(){
     var text = win.clip.read();
     if(text &amp;&amp; ( string.match(text,&quot;^%\[\]%()$&quot;) 
         || string.startWith(text,&quot;https://www.aardio.com/zh-cn/doc&quot;) ) ){
        if(!#winform.editPrompt.text) winform.editPrompt.text = &#39;\n&#39;;
        winform.editPrompt.appendText(&#39;\n&#39;,text);
     }
}
clipViewer.onDrawClipboard();

//打开 AI 搜索
winform.promptTool.link = &quot;https://www.aardio.com/zh-cn/doc/?q=/zh-cn/ai/prompt/&quot;
winform.promptTool.onHyperlinkClick = function(nmSysLink,url,id,index){
    var q = winform.editPrompt.text;
    if(#q){
        import inet.url;
        raw.execute(&quot;https://www.aardio.com/zh-cn/ai/prompt/?q=&quot;+inet.url.encode(q));
    }
    else {
        raw.execute(url);
    } 
}

//拆分界面
winform.splitter.split(winform.wndBrowser,winform.editPrompt);

winform.editPrompt.enablePopMenu(function(){
    return { 
        { /*分隔线*/ };
        { &quot;插入 aardio 语法速览&quot;;  function(id){
            winform.editPrompt.selText = &quot; [aardio 语法速览](https://www.aardio.com/zh-cn/doc/guide/language/syntax-quick-ref.html) &quot;
        }; 0}; 
        { &quot;插入 aardio 模式匹配语法文档&quot;;  function(id){
            winform.editPrompt.selText = &quot; [aardio 模式匹配语法](https://www.aardio.com/zh-cn/doc/library-guide/builtin/string/patterns.html) &quot;
        }; 0};
        { &quot;插入 web.view 使用指南（网页相关）&quot;;  function(id){
            winform.editPrompt.selText = &quot; [web.view 使用指南](https://www.aardio.com/zh-cn/doc/library-guide/std/web/view/_.html) &quot;
        }; 0};
        { &quot;插入 web.rest 使用指南（HTTP 相关）&quot;;  function(id){
            winform.editPrompt.selText = &quot; [web.rest 使用指南](https://www.aardio.com/zh-cn/doc/library-guide/std/web/rest/client.html) &quot;
        }; 0};
        { &quot;插入 多线程开发入门&quot;;  function(id){
            winform.editPrompt.selText = &quot; [多线程开发入门](https://www.aardio.com/zh-cn/doc/guide/language/thread.html) &quot;
        }; 0};  
        { &quot;插入 高级选项卡指南（多窗口）&quot;;  function(id){
            winform.editPrompt.selText = &quot; [高级选项卡指南](https://www.aardio.com/zh-cn/doc/library-guide/std/win/ui/tabs/_.html) &quot;
        }; 0};  
        { &quot;插入 plus 控件使用指南（界面美化）&quot;;  function(id){
            winform.editPrompt.selText = &quot; [plus 控件使用指南](https://www.aardio.com/zh-cn/doc/library-guide/std/win/ui/ctrl/plus.html) &quot;
        }; 0}; 
        { &quot;插入 自定义控件使用指南（界面模块化）&quot;;  function(id){
            winform.editPrompt.selText = &quot; [自定义控件使用指南](https://www.aardio.com/zh-cn/doc/library-guide/std/win/ui/ctrl/custom.html) &quot;
        }; 0};  
        { &quot;插入 调用 Python 文档&quot;;  function(id){
            winform.editPrompt.selText = &quot; [aardio 调用 Python 入门指南](https://www.aardio.com/zh-cn/doc/library-guide/ext/python/_.html) &quot;
        }; 0};
        { &quot;插入 超级热键文档&quot;;  function(id){
            winform.editPrompt.selText = &quot; [超级热键使用指南](https://www.aardio.com/zh-cn/doc/library-guide/std/key/hotkey.html) &quot;
        }; 0};  
        { &quot;检索相关 aardio 文档&quot;;  function(id){
            var q = winform.editPrompt.selText;
            if(#q){
                import inet.url;
                raw.execute(&quot;https://www.aardio.com/zh-cn/ai/prompt/?q=&quot;+inet.url.encode(q));
            }
        }; !owner.canCopy() ? 1/*_MF_GRAYED*/ : 0}; 

        { &quot;AI 搜索&quot;;  function(id){
            var q = winform.editPrompt.selText;
            if(#q){
                import inet.url;

                var url = inet.url.appendExtraInfo(&quot;http://api.aardio.com/search&quot;,{
                    q = &#39;```aardio \n&#39; + q + &#39;\n```&#39;;
                })
                raw.execute(url);
            }
        }; !owner.canCopy() ? 1/*_MF_GRAYED*/ : 0};     
    }
})

global.onError = function( err,over ){ 
    if(!over){
        import debug;
        var stack = debug.traceback(,&quot;调用栈&quot;,3);
        err = string.concat(err,stack);
    }

    if( _STUDIO_INVOKED ) {
        import win;
        win.msgboxErr(err);
    }
}

winform.spinMaxTokens.buddy = winform.editMaxTokens;
winform.spinMaxTokens.setRange(1,1024*8);
winform.spinMaxTokens.pos = config.maxTokens || 1024;
winform.spinMaxTokens.inc = 1024;
//按钮外观样式
winform.btnClear.skin({
    color={
        active=0xFF00FF00;
        default=0xFF3C3C3C;
        disabled=0xFF999999;
        hover=0xFFFF0000        
    }
})

//按钮外观样式
winform.btnSend.skin({
    color={
        active=0xFF00FF00;
        default=0xFF3C3C3C;
        disabled=0xFF999999;
        hover=0xFFFF0000        
    }
})

//按钮外观样式
winform.btnSetting.skin({
    color={
        active=0xFF00FF00;
        default=0xFF3C3C3C;
        disabled=0xFF999999;
        hover=0xFFFF0000        
    }
})

winform.btnCopy.skin({
    color={
        active=0xFF00FF00;
        default=0xFF3C3C3C;
        disabled=0xFF999999;
        hover=0xFFFF0000        
    }
})

winform.chkFix.skin({
    color={
        active=0xFF00FF00;
        default=0xFF000000;
        disabled=0xFF999999;
        hover=0xFFFF0000        
    };
    checked={
        iconText=&#39;\uF14A&#39;;  
        color={
            active=0xFF00FF00;
            default=0xFF000000;
            disabled=0xFF999999;
            hover=0xFFFF0000        
        };  
    }
})

winform.beforeDestroy = function(){
    config.maxTokens = winform.spinMaxTokens.pos;
}

winform.show();
win.loopMessage();
</code></pre>
<a href="https://www.aardio.com/zh-cn/doc/example/Web/REST/aiChat.md">Markdown 格式</a>
</body> 
</html>